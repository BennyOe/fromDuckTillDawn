# Imports ------------------------------------------------------
import idle:"io.bennyoe.ai.actions.mushroom.MushroomIdle"
import patrol:"io.bennyoe.ai.actions.mushroom.MushroomPatrol"
import chase:"io.bennyoe.ai.actions.mushroom.MushroomChase"
import attack:"io.bennyoe.ai.actions.mushroom.MushroomAttack"


import canAttack?:"io.bennyoe.ai.conditions.mushroom.CanAttack"
import shouldChase?:"io.bennyoe.ai.conditions.mushroom.ShouldChase"

# -------------------------------------------------------------
# Main-Logic:  Attack  →  Chase  →  Patrol
# -------------------------------------------------------------

# This tree is lazy and will only evaluate the conditions when a task is finished.
subtree name:"idlePatrolLoop"
  selector
    (random success: 0.4) idle duration:"uniform,1.0,1.5"
    patrol duration:"uniform,4.0,6.0"

# this tree is reactive to evaluate the conditions every tick,
# so it can switch between actions dynamically and interrupt them
root
  dynamicGuardSelector
    (canAttack?) sequence
      attack
      wait seconds:"uniform,1.5,2.0"
    (shouldChase?) chase
    $idlePatrolLoop
